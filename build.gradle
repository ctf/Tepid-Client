group 'ca.mcgill.science.ctf.tepid'
version '1.2.3'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
sourceCompatibility = JVM_TARGET
targetCompatibility = JVM_TARGET

buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN"
        classpath "ca.mcgill.tepid-commons:gradle-plugin:$TEPID_COMMONS"
    }
}

apply plugin: 'ca.mcgill.science.tepid.commons'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.github.cpicon92:Q:ccb4d8fb9d'

    compile tepidDependency.kotlin
    compile tepidDependency.rxJava
    compile tepidDependency.rxKotlin

    testCompile tepidDependency.junit
    testCompile tepidDependency.kotlinTest
    testCompile "ca.mcgill.tepid-commons:test:$TEPID_COMMONS"

    compile "ca.mcgill.tepid-commons:retrofit:$TEPID_COMMONS"
    compile "ca.mcgill.tepid-commons:models:$TEPID_COMMONS"
    compile "ca.mcgill.tepid-commons:utils:$TEPID_COMMONS"

    compile "com.bugsnag:bugsnag:3.+"

    // https://mvnrepository.com/artifact/com.dorkbox/SystemTray
    compile group: 'com.dorkbox', name: 'SystemTray', version: '3.12'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-email
    compile group: 'org.apache.commons', name: 'commons-email', version: '1.4'

    /*
     * Compression stream dependencies
     * Note that all of these are necessary, even though their removal may not all result in compile time issues
     */

    // https://mvnrepository.com/artifact/org.tukaani/xz
    compile group: 'org.tukaani', name: 'xz', version: '1.8'

    // https://mvnrepository.com/artifact/org.glassfish.jersey.core/jersey-common
    compile group: 'org.glassfish.jersey.core', name: 'jersey-common', version: '2.27'

    // https://mvnrepository.com/artifact/org.glassfish.jersey.containers/jersey-container-servlet
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.27'

    // https://mvnrepository.com/artifact/org.glassfish.jersey.inject/jersey-hk2
    compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.27'

    // https://mvnrepository.com/artifact/org.glassfish.jersey.media/jersey-media-json-jackson
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.27'


}

task copyBuilder(type: Copy) {
    into "$buildDir/libs"
    from "files"
}

task copyLibs(type: Copy) {
    into "$buildDir/libs"
    from configurations.runtime
}

task copyLibsLibs(type: Copy) {
    into "$buildDir/libs/libs"
    from configurations.runtime
}

task commandLinux(type: Exec) {
    workingDir "$buildDir/libs"
    commandLine "java", "-classpath", "./*", "ca.mcgill.science.tepid.client.Main"
}

jar {
    baseName = 'tepid'
    manifest {
        attributes 'Main-Class': 'ca.mcgill.science.tepid.client.Main'
        attributes 'Class-Path': './*'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = JVM_TARGET
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JVM_TARGET
}

task tepidWindows(type: GradleBuild) {
    tasks = ['copyBuilder', 'copyLibsLibs', 'jar']
}

task tepidLinux(type: GradleBuild) {
    tasks = ['copyLibs', 'jar']
}

task msi(type: Exec) {
    dependsOn "tepidWindows"
    workingDir "$buildDir/libs"
    environment "PRODUCTVERSION", version
    commandLine "cmd", "/c", "wix.bat"
    group 'build'
}

task run(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "ca.mcgill.science.tepid.client.Main"
}